import org.apache.tools.ant.filters.ReplaceTokens

plugins {
	id 'com.github.johnrengelman.shadow' version '8.1.1'
	id 'maven-publish'
	id 'eclipse'
	id 'java'
}

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

compileJava {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

repositories {
	mavenCentral()

	//Spigot
	maven {
		url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
	}

	// Bungeecord
	maven {
		url "https://oss.sonatype.org/content/repositories/snapshots/"
	}

	// Skript
	maven {
		url 'https://repo.skriptlang.org/releases'
	}

}

dependencies {

	//Nullable annotation
	implementation (group: 'org.eclipse.jdt', name: 'org.eclipse.jdt.annotation', version: '2.3.0')

	//Spigot/Bukkit
	implementation (group: 'org.spigotmc', name: 'spigot-api', version: '1.21.3-R0.1-SNAPSHOT')

	//Skript
	implementation (group: 'com.github.SkriptLang', name: 'Skript', version: '2.9.4') {
		transitive = false
	}

	// bStats
	shadow (group: 'org.bstats', name: 'bstats-bukkit', version: '3.1.0')

}

processResources {
	filter ReplaceTokens, tokens: ["version": project.property("version")]
	from ("lang/") {
		include '*.lang'
		into 'lang/'
	}
}

shadowJar {
	dependencies {
		include(dependency('org.bstats:bstats-bukkit'))
		include(dependency('org.bstats:bstats-base'))
	}
	relocate 'org.bstats', 'me.limeglass.khoryl.bstats'
	configurations = [project.configurations.shadow]
	archiveVersion = project.property("version")
	minimize()
}
